apply plugin:  "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"


ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}


def PUBLISH_GROUP_ID = 'com.whatspos.sdk'

def PUBLISH_ARTIFACT_ID = "paxstore-3rd-app-java-sdk"


java {
    withJavadocJar()
    withSourcesJar()
}


publishing {
    publications {
        javaSdk(MavenPublication) { //you can replace "javaSdk" with any name you like
            groupId =PUBLISH_GROUP_ID
            artifactId =PUBLISH_ARTIFACT_ID

            from components.java

            pom {
                name ='paxstore-3rd-app-java-sdk'
                // optionally artifactId can be defined here
                description ='PAXSTORE 3rd App JAVA SDK provides simple and easy-to-use service interfaces for third party developers to develop android apps on PAXSTORE.'
                url ='https://github.com/PAXSTORE/paxstore-3rd-app-java-sdk'

                scm {
                    connection = 'scm:git:github.com/PAXSTORE/paxstore-3rd-app-java-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/PAXSTORE/paxstore-3rd-app-java-sdk.git'
                    url = 'https://github.com/PAXSTORE/paxstore-3rd-app-java-sdk/tree/master'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url ='http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name= 'Derek'
                        email ='paxstore-support@paxsz.com'
                    }
                }
            }
        }
    }

    repositories {
        if (project.version.endsWith('SNAPSHOT')) {
            // snapshot can be uploaded to cloud directly
            // you need to add below to your top level build.gradle to get this dependency
            // maven { url "https://central.sonatype.com/repository/maven-snapshots/" }
            maven {
                url = "https://central.sonatype.com/repository/maven-snapshots/"
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        } else {
            // publish release version, files needed will be generated to build/staging-deploy
            // zip com directory under staging-deploy file, and go to maven portal to release
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
}


//this is an independent block
// tow sign methods supported now
signing {
    // About GPG signing, please refer to https://central.sonatype.org/publish/requirements/gpg/
//    def signingKey = file(System.getenv("GPG_ASC")).text
//    def signingPassword = System.getenv("GPG_PASSPHARSE")
//    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.javaSdk
}



